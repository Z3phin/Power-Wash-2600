
	processor 6502
        include "vcs.h"
        include "macro.h"
        include "xmacro.h"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Variables segment

        seg.u Variables
	org $80

Temp		byte	; Temp variable, used for variety of purposees
EnableMissiles  byte	; Used to store whether each missile is enabled on a scanline
			; ------XX, where bit 2 is player 1, bit 1 is player 0
ScanlineCounter	byte	; Counts which scanline we are on
tmpPF0		byte	; Temporary variable for holding playfield (PF0 register) data
tmpPF1		byte    ; Temporary variable for holding playfield (PF1 register) data
tmpPF2		byte    ; Temporary variable for holding playfield (PF2 register) data

; Scores 
Score0		byte	; Player 0 score
Score1		byte    ; Player 1 score

; Sprite Positions 
XPos0		byte	
XPos1		byte
ColP0   	byte	; Player0 colour on given scanline
ColP1		byte	; Player1 colour on given scanline

; Missile Positions
MissileY0	byte    
MissileY1	byte
MissileX0	byte
MissileX1	byte
MissileHeight0  byte
MissileHeight1  byte
MissileVel0	byte
MissileVel1	byte

; Dirt 
Dirt		ds 36 	; 6x6 bytes (Dirt map)
; Sprite Pointers

ColourPtr0	word
ColourPtr1	word
SpritePtr	word

; Playfield Pointer

PFPtr		word

; Joystick Button 
Pressed 	byte

; Score Font Buffer 2x5 array
FontBuf 	ds 10


; Constants 
SpriteHeight 	equ #16
DefaultYPos	equ #0
Player0Colour   equ #$94
Player1Colour   equ #$42

DirtHeight	equ 16	; Height of each dirt spot in pixels
NumDirtRows	equ 6	; Number of dirt rows 
BytesPerRow 	equ 6 	; Number of bytes (8 x 6 = 42 bits)
DirtPerRow 	equ 40 	; Number of Dirt spots per row 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Code segment

	seg Code
        org $f000
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	              START
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Start
	CLEAN_START
        
        
        ; Temporary setup of dirt
        ldx #0
        lda #$ff
SetupDirt
        sta Dirt,x
        txa
        inx
        cpx #BytesPerRow*NumDirtRows
        bne SetupDirt
        		
        ; Setting Player Sprite Pointers 
        
        lda #<PlayerSprite
        sta SpritePtr
        lda #>PlayerSprite
        sta SpritePtr+1
        
        lda #<ColourFrame0
        sta ColourPtr0
        lda #>ColourFrame0
        sta ColourPtr0+1
          
        lda #<ColourFrame1
        sta ColourPtr1
        lda #>ColourFrame1
        sta ColourPtr1+1
        
        ; Intitial Scores
        lda #0
        sta Score0
        sta Score1
        sta MissileHeight0
        
        ; Initial Player and missile Y Positions
        lda #255	; at position 0 and then adding more the player sprites move upwards
        sta MissileY0
        sta MissileY1
        
        lda #0
        sta MissileVel0
        sta MissileVel1
        
        ; Initial Plaayer and missile X positions
       	lda #40
        sta XPos0
        sta MissileX0
        lda #112
        sta XPos1
        sta MissileX1
        lda #1
        sta VDELP0	; Set delay for displaying P0 until GRP1 is set
         
        lda XPos0
        ldx #0
        jsr SetHorizPos
        
        lda XPos1
        ldx #1
        jsr SetHorizPos
        
        ldx #1
        sta WSYNC
        sta HMOVE

NextFrame
        lsr SWCHB	; test Game Reset switch
        bcc Start	; reset?
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	         VERTICAL SYNC
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

        
; 1 + 3 lines of VSYNC
	VERTICAL_SYNC
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	         VERTICAL BLANK
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
        
; 37 lines of underscan
	TIMER_SETUP 37 
      
       	; Setup the scores ready for display  
        lda Score0
        ldx #0
        jsr GetBCDBitmap
        lda Score1
        ldx #5
        jsr GetBCDBitmap
          
        ; Set the horizontal position of players
        lda XPos0
        ldx #0
        jsr SetHorizPos
        
        lda XPos1
        ldx #1
        jsr SetHorizPos
        
        ; Offset the missile0 to the center of sprite0
        lda XPos0
        clc
        adc #4
        ldx #2
        jsr SetHorizPos
        
        ; Offset the missile1 to the center of sprite1
        lda XPos1
        adc #4
        ldx #3
        jsr SetHorizPos
        
        
        ; Move the missile upwards constantly by one scanline every frame until the given Y position of the missile
        ; Reset the missile to the player's position if it reaches the maximum height
        lda MissileY0
        sec
        sbc MissileVel0 ; decrease y position of missile by 4 per frame
        sbc MissileVel0
        sbc MissileVel0
        sbc MissileVel0
        
        cmp #115       ; Maximum height of the missile
        bcs .NoReset  
        lda Pressed
        lsr
        bcs .NoMoveY
        ; Reset the missile to the player with a velocity of 0
        lda #0
        sta MissileVel0 ; 0

        lda DefaultYPos
.NoReset
        sta MissileY0  
.NoMoveY

; Move the missile upwards constantly by one scanline every frame until the given Y position of the missile
        ; Reset the missile to the player's position if it reaches the maximum height
        lda MissileY1
        sec
        sbc MissileVel1 ; decrease y position of missile by 4 per frame
        sbc MissileVel1
        sbc MissileVel1
        sbc MissileVel1
        
        cmp #115       ; Maximum height of the missile
        bcs .NoReset1 
        lda Pressed
        lsr
        lsr
        bcs .NoMoveY1
        ; Reset the missile to the player with a velocity of 0
        lda #0
        sta MissileVel1 ; 0

        lda DefaultYPos
.NoReset1
        sta MissileY1  
.NoMoveY1

	; Setup background colour and scoremode ready for scoreboard
        lda #$00
        sta COLUBK
        lda #5
        sta CTRLPF	; Symmetry

        sta WSYNC
        sta HMOVE ; Apply horizontal offset 
       
        TIMER_WAIT
; 192 lines of frame
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	            DRAWING
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
	
        TIMER_SETUP 192 
        jsr DrawScoreboard
        
        ; Set Background to blue
        lda #$ba
        sta COLUBK
        
        ; DRAW PLAYER LOOP
        ; ----------------
        
        lda SpriteHeight
        sta Temp
DrawSpriteLoop
	ldy Temp
        
        lda (ColourPtr0),y	; Colour for both lines
        sta ColP0		; A -> ColP0
        lda (SpritePtr),y	; Bitmap for first line
        sta GRP0		; A -> [GRP0] (delayed due to VDELP0)
        
        lda (ColourPtr1),y
        tax
        lda (SpritePtr),y
        tay
        
        lda ColP0 
        sta WSYNC
        ; HBLANK
        sty GRP1	;GRP0 also updated due to VDELP0
        ; Store the player colours
        stx COLUP1
        sta COLUP0
        
        dec Temp
        
        lda Temp 
        sta WSYNC
        
        bne DrawSpriteLoop
        
        lda #0
        sta GRP0
        sta GRP1
        lda #$aF
        sta COLUP0
        sta COLUP1
        
        ; DRAW MISSILES LOOP (to be combined with playfield graphics loop)
        ; ------------------
        
        ldy #0
DrawMissileLoop
        sty Temp
        lda MissileHeight0
        sec			; Set carry
        sbc Temp	        ; 0 - (-1)
        lda #2
        bcs .EnableMissile0
        lda #0
.EnableMissile0
        sta EnableMissiles
        
        lda MissileHeight1	; TODO Replace with 1
        sec			; Set carry
        sbc Temp	        ; 0 - (-1)
        bcc .EnableMissile1
        inc EnableMissiles
.EnableMissile1
        sta WSYNC
        ;HBLANK
	lda EnableMissiles
        sta ENAM0
        asl
        sta ENAM1
        
        iny
        cpy #40
        bne DrawMissileLoop
        
        
        
        ; PLAYFIELD GRAPHICS LOOP
        ; ------------------
        
        lda #$54
        sta COLUPF
        
        ; Y contains the dirt row we are on. 
        
        ldy #$ff ; Start at -1 
        ldx #$ff
ScanLoopA
	iny ; Go to next brick row 
        lda #DirtHeight
        sta Temp
        cpy #NumDirtRows
        bcs DoneDirtDraw
ScanLoopB
        sta WSYNC
        lda Dirt+NumDirtRows*0,y
        sta PF0
        lda Dirt+NumDirtRows*1,y
        sta PF1
        lda Dirt+NumDirtRows*2,y
        sta PF2
      
        inx
        nop
       
        lda Dirt+NumDirtRows*3,y
        sta PF0
        lda Dirt+NumDirtRows*4,y
        sta PF1
        lda Dirt+NumDirtRows*5,y
        sta PF2
               
        dec Temp
        bne ScanLoopB	; All dirt spots done on current line? 
        beq ScanLoopA	; Branch always taken
        
DoneDirtDraw
	sta WSYNC
  	lda #0
        sta PF0
        sta PF1
        sta PF2
         
        TIMER_WAIT

; 29 lines of overscan

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	            OVERSCAN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	TIMER_SETUP 29
       
        lda #0
        sec
        sbc MissileY0
        sta MissileHeight0
        
        lda #0
        sec
        sbc MissileY1
        sta MissileHeight1
        
       
        jsr JoystickButton1
        jsr JoystickButton
        jsr FireButton0
        jsr FireButton1
        jsr MoveJoystick0
        jsr MoveJoystick1
        
        TIMER_WAIT
; total = 262 lines, go to next frame
        jmp NextFrame

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 					Subroutines
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Scoreboard Subroutines
; ----------------------
; Credit to 8Bitworkshop (Steven Hugg)

DrawScoreboard subroutine
	; Put playfield into score mode
        
        lda #%00010010
        sta CTRLPF
        lda #$98   ; Slightly lighter colour than player0 sprite
        sta COLUP0 ; left colour
        lda #$46   ; Slightly lighter colour than player1 sprite		
        sta COLUP1 ; right colour	
        ; Draw digits
        ldy #0
ScanLoop1a
        sta WSYNC ; 76
        tya
        lsr	   ; Divide Y by two for double-height lines 	
        tax	   ; A -> X
        lda FontBuf+0,x
        sta PF1		; set left score bitmap	
        SLEEP 28
        lda FontBuf+5,x
        sta PF1		; set right score bitmap
        iny
        cpy #10
        bcc ScanLoop1a
        
        ; Clear playfield
        lda #0
        sta WSYNC
        sta PF1
        ; Turn playfield reflection off (and turn score mode off)
        lda #%00010100
        sta CTRLPF
        rts

        
; GetBCDBitmap
; Fetches the bitmap data for two digits of a
; BCD-encoded number, storing it in addresses
; FontBuf+x to FontBuf+4+x
GetBCDBitmap subroutine 
	; Fetch the bytes for the 1st digit
        pha		; save original BCD number
        and #$0F	; mask out the least significant number
        sta Temp
        asl 
        asl
        adc Temp 	; multiply by 5
        tay		; A -> Y
        lda #5
        sta Temp	; count down from 5
.loop1
	lda DigitsBitmap,y
        and #$0F	; mask out left most digit
        sta FontBuf,x 
        iny
        inx
        dec Temp
        bne .loop1
        
        ; Second Digit 
        
	pla 		; restore original BCD number
        lsr
        lsr
        lsr
        lsr		;shift right by 4
        sta Temp
        asl 
        asl
        adc Temp	; multiply by 5
        tay 		; A -> Y
        dex
        dex
        dex
        dex
        dex
        lda #5
        sta Temp 	; count down from 5
.loop2
	lda DigitsBitmap,y
        and #$F0	; make out rightmost digit
        ora FontBuf,x	; combine left and right digts
        sta FontBuf,x	; store combined digits
        iny
        inx
        dec Temp
        bne .loop2
        rts
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;        
; 		Joystick Input Subroutines 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Player 0 Movement
; ----------------

; Read joystick movement and apply to object 0
MoveJoystick0
; Move horizontally
        ldx XPos0
	lda #%01000000	;Left?
	bit SWCHA
	bne SkipMoveLeft
        cpx #1
        bcc SkipMoveLeft
        dex
SkipMoveLeft
	lda #%10000000	;Right?
	bit SWCHA 
	bne SkipMoveRight
        cpx #153
        bcs SkipMoveRight
        inx
SkipMoveRight
	stx XPos0
	rts
        
; Player 1 Movement
; ----------------
        
MoveJoystick1
; Move horizontally
        ldx XPos1
	lda #$04	;Left?
	bit SWCHA
	bne SkipMoveLeft1
        cpx #1
        bcc SkipMoveLeft1
        dex
SkipMoveLeft1
	lda #$08	;Right?
	bit SWCHA 
	bne SkipMoveRight1
        cpx #153
        bcs SkipMoveRight1
        inx
SkipMoveRight1
	stx XPos1
                
	rts

; Player 0 Button
; ---------------

FireButton0 subroutine
	bit INPT4	  ; read Joystick 1 button
        bmi .NoFireButton ; bit 7 set?
        lda MissileVel0   ; Check if missile is already fired. 
        ror 		  ; Move value into carry to be checked easily
        bcs .NoFireButton ; Skip firing the missile (do not add on nto velocity)
        inc MissileVel0
.NoFireButton
	rts

; Player 1 Button
; ---------------

FireButton1 subroutine
	bit INPT5	  ; read Joystick 1 button
        bmi .NoFireButton ; bit 7 set?
        lda MissileVel1   ; Check if missile is already fired. 
        ror ; Move value into carry to be checked easily
        bcs .NoFireButton ; Skip firing the missile 
        inc MissileVel1
.NoFireButton
	rts


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 		Set Horizontal Position
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; The X register contains the index of the desired object:
;  X=0: player 0
;  X=1: player 1
;  X=2: missile 0
;  X=3: missile 1
;  X=4: ball
SetHorizPos
	sta WSYNC	; start a new line
        bit 0		; waste 3 cycles
	sec		; set carry flag
DivideLoop
	sbc #15		; subtract 15
	bcs DivideLoop	; branch until negative
	eor #7		; calculate fine offset
	asl
	asl
	asl
	asl
	sta RESP0,x	; fix coarse position
	sta HMP0,x	; set fine offset
	rts		; return to caller
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 		      Miscellaneous 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;        


; This will eventually be replaced by collisions increasing the score
JoystickButton subroutine
        bit INPT4
        bmi .SetNotPressed ; if the button is not pressed, set the pressed flag to 0
       	lda Pressed	   ; Otherwise, if it is already pressed, do not do anything 
        lsr
        bcs .skip           
        inc Pressed        ; Otherwise, set the flag nad add one to the score 
        
        ; Add one to the score
        clc
        sed
        lda Score0
        adc #1	
        sta Score0
        cld
        ; end add one to score
        
        jmp .skip
.SetNotPressed
	lda Pressed
        eor %00000001
        sta Pressed
.skip	
	rts
        
JoystickButton1 subroutine
        bit INPT5
        bmi .SetNotPressed ; if the button is not pressed, set the pressed flag to 0 
	lda Pressed	   ; Otherwise, if it is already pressed, do not do anything
        lsr
        lsr
        bcs .skip           
        lda #2        ; Otherwise, set the flag nad add one to the score 
        sta Pressed
        
        ; Add one to the score
        clc
        sed
        lda Score1
        adc #1	
        sta Score1
        cld
        ; end add one to score
        
        jmp .skip
.SetNotPressed
	lda Pressed
        eor %00000010
        sta Pressed
.skip	
	rts
        
	org $FF00

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;			     BITMAPS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


; Scoreboard digits bitmap
; ------------------------

; Credit to 8BitWorkshop
; Bitmap pattern for digits
DigitsBitmap ;;{w:8,h:5,count:10,brev:1};;
        .byte $EE,$AA,$AA,$AA,$EE
        .byte $22,$22,$22,$22,$22
        .byte $EE,$22,$EE,$88,$EE
        .byte $EE,$22,$66,$22,$EE
        .byte $AA,$AA,$EE,$22,$22
        .byte $EE,$88,$EE,$22,$EE
        .byte $EE,$88,$EE,$AA,$EE
        .byte $EE,$22,$22,$22,$22
        .byte $EE,$AA,$EE,$AA,$EE
        .byte $EE,$AA,$EE,$22,$EE
;;end


;      Player 0 Bitmap
; ------------------------

;---Graphics Data from PlayerPal 2600---

PlayerSprite
        .byte #0
        .byte #%01111110;$00
        .byte #%00111100;$00
        .byte #%00111100;$94
        .byte #%00111100;$94
        .byte #%00111100;$94
        .byte #%00111100;$94
        .byte #%00111100;$00
        .byte #%01111110;$94
        .byte #%01111110;$94
        .byte #%01111110;$94
        .byte #%01111110;$94
        .byte #%01111110;$94
        .byte #%00111100;$0E
        .byte #%00100100;$94
        .byte #%00100100;$94
        .byte #%00111100;$94
;---End Graphics Data---


;---Color Data from PlayerPal 2600---

ColourFrame0
        .byte #$0; ; Colour of the missile
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte #$00;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte #$0E;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        .byte Player0Colour;
        
ColourFrame1
        .byte #00 ; Colour of the missile
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte #$00;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte #$0E;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        .byte Player1Colour;
        
;---End Color Data---


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Epilogue

	org $fffc
        .word Start	; reset vector
        .word Start	; BRK vector
